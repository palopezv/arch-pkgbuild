# Maintainer: P. A. LÃ³pez-Valencia <palopezv AT gmail DOT com>
# Based on work by Nuno Araujo <nuno.araujo@russo79.com>

pkgname=snappy-player-git
pkgver=0.3.7.r22.g4fc7f4b
pkgrel=1
pkgdesc="Clutter-based media player with a minimalistic interface"
arch=('x86_64')
url="http://projects.gnome.org/snappy/"
license=('GPL')
depends=(gstreamer clutter-gst clutter-gtk libxtst 
	desktop-file-utils hicolor-icon-theme)
makedepends=(gnome-common)
source=("$pkgname::git://git.gnome.org/snappy")
install=$pkgname.install
conflicts=('snappy-player')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
    git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
    }

prepare() {
  cd "$srcdir/$pkgname"
  sed -i "s/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/" configure.ac
  sed -i "s/Exec=snappy/Exec=snappy-player/" data/snappy.desktop
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh --enable-dbus --disable-static --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname"
  make -k check
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
  mv $pkgdir/usr/bin/snappy $pkgdir/usr/bin/snappy-player
  mv $pkgdir/usr/share/applications/snappy.desktop $pkgdir/usr/share/applications/snappy-player.desktop
}

# vim:set ts=2 sw=2 et:
