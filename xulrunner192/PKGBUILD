# Maintainer: Matt Parnell/ilikenwf <parwok@gmail.com>
# Thanks to loki for fixes!
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: David Roheim <david dot roheim at gmail dot com>
pkgname=xulrunner192
pkgver=1.9.2.28
_ffoxver=3.6.28
pkgrel=4
pkgdesc="Mozilla Runtime Environment"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'nss' 'libxt' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib' 'libevent' 'sqlite>=3.7.4' 'libnotify>=0.4')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui' 'python2' 'wireless_tools' 'autoconf2.13')
url="http://wiki.mozilla.org/XUL:Xul_Runner"

source=(ftp://mirrors.kernel.org/slackware/slackware-13.1/patches/source/mozilla-firefox/firefox-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        fix-mozilla-launcher.patch
        xulrunner-version.patch
        xulrunner-png14.patch
        xulrunner-png15.patch
        enable-x86_64-tracemonkey.patch
        offsetof.patch
        python2.7.patch
        file_util.patch
        message_pump_libevent.patch
        file_descriptor_set_posix.patch
        file_util_linux.patch
        time_posix.patch
        scopefixes.patch)

sha256sums=('a6c4bc52095194428cf3b17341b68ac473b7654984000c45d1be3eae55d78260'
            '6fd7d19c8e874b945f1a7294f8f719a50fb2660bce327f6cbc2e9669e283bacb'
            'fd220b9dc634930ef0abab8c98e7b114368c851eedec1964f4c98a7519116138'
            'd4948cc5878b2100b4d19b0fbc09119c34377593c5847678d5788db2b4e0fe43'
            '6c36e825ad6a35f33561678e27995e26601a91f6951af3144908560ef513d134'
            '25885629767e42d06d93083b5748c95e94304bf5637997bf6446ab9b3893d1a5'
            '0eae2229b5a782baa0fefa439f3a996e28cfbec98271cae23696d21c842d4af6'
            '20f558efbf4ed1960b390c353c7eeb94108fcdb8aaf33f1fea6caf44468aeb7c'
            '51e2ca82434e3328d2dd6e0140ebdba0f10bd413cd94ee735a9881ffd10ff6a5'
            '5db39daa00d0aaef2a7784defbfb0c8d02689ff71f6c1f411d53b635ce87a4e7'
            '77de9b4b5542738c707930ec4f709dd86b725789996c453ceaa09c75263e7a1b'
            'fafb326fcd903cec460ef089ac204eb6b5c1d5bce89513ee40071bee8c064e24'
            'f4d0051e22970d9c98a8da3d6577fab1bb8132017adf0be08f38999c71824d34'
            '3113a6cba86e70a93bfded0082495298553c0587e93904983e868f33130d9606'
            '079193001b0fff752147cdd8be4d2cff5bc65ab8a5fe80fc29ffc888ec54f4c6'
            '54f0e2069af3b53144c58f304be5152104b87e7ecf7371eb520bfc8acd6d321d')

prepare() {
  cd "${srcdir}/mozilla-1.9.2"
  cp "${srcdir}/mozconfig" .mozconfig

  patch -Np1 -i "${srcdir}/offsetof.patch" || return 1

  # Fix C++ 4.7 compile errors
  patch -Np1 -i "${srcdir}/file_util.patch" || return 1
  patch -Np1 -i "${srcdir}/message_pump_libevent.patch" || return 1
  patch -Np1 -i "${srcdir}/file_descriptor_set_posix.patch" || return 1
  patch -Np1 -i "${srcdir}/file_util_linux.patch" || return 1
  patch -Np1 -i "${srcdir}/time_posix.patch" || return 1
  patch -Np0 -i "${srcdir}/scopefixes.patch" || return 1

  # fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch" || return 1

  # Fix stub launcher - archlinux
  patch -Np0 -i "${srcdir}/fix-mozilla-launcher.patch" || return 1

  # Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch" || return 1

  # Fix compile with libpng 1.4
  patch -Np0 -i "${srcdir}/xulrunner-png14.patch" || return 1

  # Fix compile with libpng 1.5
  patch -Np0 -i "${srcdir}/xulrunner-png15.patch" || return 1

  # Tracemonkey for x86_64
  patch -Np0 -i "${srcdir}/enable-x86_64-tracemonkey.patch" || return 1

  # python2.7
  patch -Np0 -i "${srcdir}/python2.7.patch" || return 1

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS
}

build()
{
  cd "${srcdir}/mozilla-1.9.2"
  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "${srcdir}/mozilla-1.9.2"
  make -j1 DESTDIR="${pkgdir}" install

  # Remove included dictionaries, add symlink to system myspell path.
  # Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "${pkgdir}/usr/lib/xulrunner-1.9.2/dictionaries"
  ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/xulrunner-1.9.2/dictionaries"

  rm -f "${pkgdir}/usr/bin/xulrunner"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}
