_name=fonttools
pkgname=python-"$_name"-git
pkgver=2.5.r1471
pkgrel=2
pkgdesc='Modify OpenType and TrueType fonts and convert them to and from XML'
arch=('any')
url="https://github.com/behdad/$_name"
license=('MIT')
depends=('python')
makedepends=('cython')
provides=("python-$_name")
conflicts=("python-$_name")
source=("git+https://github.com/behdad/$_name.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_name"
  printf "%s.r%s" \
    "$(git describe --abbrev=0 | sed 's/^v//')" \
    "$(git rev-list --count HEAD)"
}

package() {
  cd "$srcdir/$_name"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 {,"$pkgdir/usr/share/licenses/$pkgname/"}LICENSE.txt
  mv "$pkgdir/usr/bin/ttx" "$pkgdir/usr/bin/ttx3"
  mv "$pkgdir/usr/share/man/man1/ttx.1" "$pkgdir/usr/share/man/man1/ttx3.1"
}

