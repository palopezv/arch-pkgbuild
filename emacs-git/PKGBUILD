# Maintainer: P. A. LÃ³pez-Valencia <palopezv at gmail dot com>

#
# See <https://lists.gnu.org/archive/html/emacs-devel/2014-01/msg00005.html> 
# for the start of the discussion on why emacs development moves to git.
#
# See <https://lists.gnu.org/archive/html/emacs-devel/2014-01/msg00015.html>
# for the actual community proposal about moving to git with RMS's blessing.
#
# TIP: Don't run this on a tmpfs unless you have oodles of RAM.
#      The initial cloned repo is huge, 1.2GB or more. Run
#      "git gc --aggressive" on it to reduce its size drastically,
#      but it will take a long time in a low-spec machine or if you
#      have less than 4GB RAM.
#

pkgname=emacs-git
pkgver=24.3.50.2d318ae
pkgrel=1
pkgdesc="GNU Emacs. Official git trunk."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/"
license=('GPL')
groups=('devel' 'editors' 'i18n' 'network' 'office' 'science' 'system' 'x11')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libotf' 'm17n-lib' 
	 'gtk3' 'hicolor-icon-theme' 'desktop-file-utils'
	 'alsa-lib' 'imagemagick')
makedepends=('git')
conflicts=('emacs' 'emacs-nox' 'emacs-otf' 'emacs-cvs' 'emacs-bzr')
install="$pkgname".install
source=("$pkgname::git://git.savannah.gnu.org/emacs.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s" "$(grep AC_INIT configure.ac | sed -e 's/^.\+\ \(.\+\))/\1/')" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"
  ./autogen.sh                                                            
}

build() {
  cd "$srcdir/$pkgname"
  ac_cv_lib_gif_EGifPutExtensionLast=yes \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --enable-link-time-optimization \
    --with-sound=alsa \
    --without-gconf \
    --with-x-toolkit=gtk3 \
    --with-xft
  make bootstrap
  cd "$srcdir/$pkgname/lisp"
  make autoloads
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  # remove conflict with texinfo
  rm "$pkgdir"/usr/share/info/info.info.gz
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/ -exec chown root:root {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}

# vim:set ts=2 sw=2 et:

