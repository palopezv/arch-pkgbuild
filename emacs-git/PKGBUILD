# See AUR interface for current maintainer.

#
# TIP: Don't run this on a tmpfs unless you have oodles of RAM.
#      The initial cloned repo is 200MB. As time passes it will
#      grow more.
#

pkgname=emacs-git
pkgver=25.0.50.r120753
pkgrel=1
pkgdesc="GNU Emacs. Official git trunk."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/"
license=('GPL')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libotf' 'm17n-lib' 
	 'gtk3' 'hicolor-icon-theme' 'desktop-file-utils' 
	 'alsa-lib' 'imagemagick' 'gnutls')
makedepends=('git')
conflicts=('emacs')
provides=('emacs')
install="$pkgname".install
source=("$pkgname::git://git.savannah.gnu.org/emacs.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.r%s" \
    "$(grep AC_INIT configure.ac | sed -e 's/^.\+\ \([0-9]\+\.[0-9]\+\.[0-9]\+\).\+$/\1/')" \
    "$(git rev-list --count HEAD)"
}


prepare() {
  cd "$srcdir/$pkgname"
  ./autogen.sh                                                            
}

build() {
  cd "$srcdir/$pkgname"
  ac_cv_lib_gif_EGifPutExtensionLast=yes \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --pdfdir=/usr/share/doc/emacs/pdf \
    --with-sound=alsa \
    --without-gconf \
    --with-x-toolkit=gtk3 \
    --with-xft

  make bootstrap
  # cd "$srcdir/$pkgname/lisp"
  # make autoloads
  # Before enabling "make docs" you need to install texlive, either from
  # the repos or locally, using texlive-dummy from AUR. PDF, yay!
  #make docs
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
  # Before enabling "make install-doc" you need to install texlive, either 
  # from the repos or locally, using texlive-dummy from AUR.
  # make DESTDIR="$pkgdir/" install-doc

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1.gz}
  # remove conflict with texinfo
  rm "$pkgdir"/usr/share/info/info.info.gz
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/ | xargs chown root:root
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}

# vim:set ft=sh ts=2 sw=2 et:
