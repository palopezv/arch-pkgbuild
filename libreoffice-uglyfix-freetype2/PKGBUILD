pkgname=libreoffice-uglyfix-freetype2
pkgver=2.5.3
pkgrel=1
pkgdesc="Installs freetype2 .so files in the LibreOffice directory to fix font ugliness"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'libpng' 'libreoffice-common')
options=(!docs)
source=(
  "http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
  'libreoffice.patch'
  'freetype-2.2.1-enable-valid.patch'
  'freetype-2.5.1-enable-spr.patch'
  'freetype-2.5.1-enable-sph.patch')    
sha1sums=('d3c26cc17ec7fe6c36f4efc02ef92ab6aa3f4b46'
          'b68c5328b79b93c3a5e385a66ad6113640c6a3f0'
          'f279d922a873d62a8af50bfc873051839d194dca'
          '13ee8d558593db991ad29fa090b461f914536104'
          'c31fa3d342ead56f3acfa1f267b474a7686d0014')

prepare()
{
  cd "$srcdir/freetype-$pkgver"

  patch -Np1 -i "$srcdir/libreoffice.patch"

  # Arch patches
  #
  # Enable font validity checking (bad fonts can crash your
  # whole graphics stack)
  #
  patch -Np1 -i "$srcdir/freetype-2.2.1-enable-valid.patch"
  #
  # Enable subpixel rendering
  #
  patch -Np1 -i "$srcdir/freetype-2.5.1-enable-spr.patch"

  # Disabled for now due to resistance
  # Kept here for easier rebuilds via ABS
  # https://bugs.archlinux.org/task/35274
  #
  # Enable subpixel hinting.
  #
  #patch -Np1 -i "$srcdir/freetype-2.5.1-enable-sph.patch"

}
      
build()
{
  cd "$srcdir/freetype-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

package()
{
  cd "$srcdir/freetype-$pkgver"
  install -d "$pkgdir"/usr/lib/libreoffice/program
  cp -aP "$srcdir/freetype-$pkgver"/objs/.libs/libfreetype.so* \
    "$pkgdir"/usr/lib/libreoffice/program
}
